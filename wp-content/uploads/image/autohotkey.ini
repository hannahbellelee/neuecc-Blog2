;AutoHotKey for 日本語キーボード,3ボタンマウス
;LastUpdate 2008/10/27
;http://neue.cc/
;修飾キー ^ Ctrl , ! Alt , + Shift , # Win
;改行は`r , 変数利用時は%hoge%
;コメントは先頭に;

;関数

;文字列貼り付け用関数
HotString(msg)
{
	bk=%ClipboardAll%
	Clipboard=%msg%
	Send,^v
	Clipboard=%bk%
}

;文字列挟み込み用関数
SandString(start,end)
{
	bk=%ClipboardAll%
	Clipboard=
	Send,^c
	if(Clipboard!="")
		Clipboard = %start%%clipboard%%end%
	else
		Clipboard = %start%%end%
	Send,^v
	Clipboard=%bk%
}

;クリップボードの中身で文字列挟み込み用関数
ClipSandString(start,end)
{
	bk=%ClipboardAll%
	Clipboard = %start%%clipboard%%end%
	Send,^v
	Clipboard=%bk%
}

;Window移動用関数
MoveWindow(xStep,yStep)
{
	WinGetPos,X,Y,,,A
	X:=X+xStep
	Y:=Y+yStep
	WinMove A,,%X%,%Y%
}

;一行コピーもどき
LineCopy()
{
	Clipboard=
	Send,^c
	if(Clipboard="")
	{
		Send,{End}+{Home}
		Send,^c{Home}
	}
}

;CapsLock
;sc03a
;弄ると不具合あるので放置

;無変換
;十字キーゾーン
vk1Dsc07B & e::Send,{Up}
vk1Dsc07B & s::Send,{Left}
vk1Dsc07B & d::Send,{Down}
vk1Dsc07B & f::Send,{Right}
vk1Dsc07B & w::Send,{PgUp}
vk1Dsc07B & r::Send,{PgDn}
vk1Dsc07B & q::Send,{Home}
vk1Dsc07B & a::Send,{End}
vk1Dsc07B & t::Send,^{Home}
vk1Dsc07B & g::Send,^{End}
vk1Dsc07B & z::Send,^{Left}
vk1Dsc07B & c::Send,^{Right}
vk1Dsc07B & x::Send,{End}+{Home}
vk1Dsc07B & v::Send,{End}{Enter}
vk1Dsc07B & b::Send,{Up}{End}{Enter}
;タスク切り替え
vk1Dsc07B & Ctrl:: AltTab
vk1Dsc07B & Shift:: ShiftAltTab
;矢印キー(マルチディスプレイでのウィンドウ移動)
;vk1Dsc07B & Left::WinMove A,,-1200,0 ;絶対座標なのでWindowSpyで座標計測して代入してください
;vk1Dsc07B & Right::WinMove A,,102,0 ;同上
;マウスとの組み合わせ
vk1Dsc07B & LButton::Send,^c
vk1Dsc07B & RButton::Send,^v
vk1Dsc07B & MButton::Send,{BS}
;テンキーもどき(別にいらないけどネタとして）
vk1Dsc07B & m::Send,0
vk1Dsc07B & ,::Send,{,}
vk1Dsc07B & .::Send,{.}
vk1Dsc07B & j::Send,1
vk1Dsc07B & k::Send,2
vk1Dsc07B & l::Send,3
vk1Dsc07B & u::Send,4
vk1Dsc07B & i::Send,5
vk1Dsc07B & o::Send,6
vk1Dsc07B & 7::Send,7
vk1Dsc07B & 8::Send,8
vk1Dsc07B & 9::Send,9

;変換
vk1Csc079 & a::SandString("<a href=>","</a>")
vk1Csc079 & r::SandString("#region`r","`r#endregion")
vk1Csc079 & Left::MoveWindow(-100,0)
vk1Csc079 & Right::MoveWindow(100,0)
vk1Csc079 & Up::MoveWindow(0,-100)
vk1Csc079 & Down::MoveWindow(0,100)
 
;ひらがなカタカナ
;vkF2sc070:: 
;CapsLockと同じくAutoHotKeyで変更すると不具合有り

;右上のキー
Ins::^+Esc
;ScrollLock:: Run,gyazowin.exe
Pause::WinClose,A

;Windowsキー
#Left::MoveWindow(-1,0)
#Right::MoveWindow(1,0)
#Up::MoveWindow(0,-1)
#Down::MoveWindow(0,1)
#LButton::^Left
#RButton::^Right
#MButton::WinSet,AlwaysOnTop,TOGGLE,A

;Applicationキー
AppsKey & c::Run,calc.exe
AppsKey & n::Run,notepad.exe
AppsKey & d::Run,cmd.exe
AppsKey & s::Run,SnippingTool.exe
;AppsKey & x::
;	Run,アプリへのパス\XTMemo.exe
;	WinWait,ahk_class  TXTMainForm
;	WinMove ahk_class  TXTMainForm,,0,0,842,1200
;	return

;マウス中ボタン
MButton::!F4
^MButton::^!F4
!MButton::WinMinimize,A
+MButton::
	bk=%ClipboardAll%
	Clipboard=
	Send,^c
	SplitPath, Clipboard, name, dir, ext, noext, drive
	IfInString,drive,http://
		Run,%Clipboard%
	else If(drive!="")
		Run,%dir%
	else if(Clipboard!="")
		Run,http://www.google.com/search?q=%Clipboard%
	Clipboard=%bk%
	return

;アプリケーション固有設定

;Excel
#IfWinActive,ahk_class XLMAIN
+Enter::Send,{F2}
^Enter::Send,!{Enter}
^Left::Send,^{PgUp}
^Right::Send,^{PgDn}
#IfWinActive

; Firefox
#IfWinActive,ahk_class MozillaUIWindowClass
^Up::Send,{PgUp}
^Down::Send,{PgDn}
;^Left::Send,{F1} ;ショートカットカスタマイズでタブの左へ移動をF1に振ってください
;^Right::Send,{F2} ;同上
MButton::Send,^{w}
^MButton::Send,!{F4}
#IfWinActive

;Notepad++
#IfWinActive,ahk_class Notepad++
F5::Reload ;AutoHotKeyのスクリプト更新
^Right::^Tab
^Left::^+Tab
^c:: LineCopy()
#IfWinActive

;DOS窓
#IfWinActive,ahk_class ConsoleWindowClass
^v::ControlSend,,%clipboard%
#IfWinActive

;Visual Studio
#IfWinActive,ahk_class wndclass_desked_gsk
^w::^F4 ;タブを閉じる
MButton::Send,^{F4}
^MButton::Send,!{F4}
#IfWinActive